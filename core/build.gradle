import de.thetaphi.forbiddenapis.gradle.CheckForbiddenApis

plugins {
    id 'jacoco'
    id 'org.spongepowered.configurate.build.component'
    id 'eclipse'
}

description = """
A simple configuration library for Java applications that can handle a variety of formats and
provides a node-based data structure able to handle a wide variety of configuration schemas
""".replace('\n', ' ')

configurate.useAutoValue()
dependencies {
    api libs.geantyref
    api libs.option
    testImplementation libs.checkerQual // needs to be on runtime classpath on Java 8 for object mapper
    compileOnlyApi libs.checkerQual
    testImplementation libs.guava
}

tasks.jar {
    manifest.attributes["Automatic-Module-Name"] = "${project.group}.configurate"
}

tasks {
    test.configure {
        finalizedBy(jacocoTestReport)
    }
    jacocoTestReport.configure {
        dependsOn(test)
    }
}

sourceSets {
    main {
        multirelease {
            alternateVersions(
                // 9, // VarHandles // TODO: temporarily disabled, cannot write final fields
                10, // immutable collections
                16 // FieldDiscoverer for records
            )
            // moduleName("org.spongepowered.configurate") // TODO: blocked by geantyref release
        }
    }

    test {
        multirelease {
            alternateVersions(16)
            configureVariants {
                tasks.named(variant().getTaskName("forbiddenApis", null), CheckForbiddenApis) {
                    enabled = false // fails to load records classes
                    targetCompatibility = "16"
                }
            }
        }
    }
}
