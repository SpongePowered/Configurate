plugins {
    id 'groovy-gradle-plugin'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    if (JavaVersion.current() > JavaVersion.VERSION_1_10) {
        options.release = 8
    }
}

dependencies {
    constraints {
        implementation("com.github.siom79.japicmp:japicmp") {
            version { require "0.15.+" }
            because "Use a newer version than the japicmp gradle plugin provides"
        }
    }
    def indraVersion = "2.1.1"

    implementation gradleApi()
    api "net.kyori:indra-common:$indraVersion"
    api "net.kyori:indra-publishing-sonatype:$indraVersion"
    api "net.kyori:indra-git:$indraVersion"
    api "gradle.plugin.org.cadixdev.gradle:licenser:0.6.1"
    api "net.ltgt.gradle:gradle-errorprone-plugin:2.0.2"
    api "net.ltgt.gradle:gradle-nullaway-plugin:1.3.0"
    api "me.champeau.gradle:japicmp-gradle-plugin:0.3.1"
    api "de.thetaphi:forbiddenapis:3.2"
    api "com.diffplug.gradle:goomph:3.35.0"
    implementation "com.google.guava:guava:31.1-jre"
}
