pluginManagement {
    repositories {
        maven {
            name = "stellardriftReleases"
            url = "https://repo.stellardrift.ca/repository/internal/"
            mavenContent { releasesOnly() }
        }
        mavenCentral()
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

dependencyResolutionManagement {
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
    repositories {
        pluginManagement.repositories.each(it.&add)
        // Allow checkstyle only to be resolved from mavenLocal if set to a snapshot
        if (settings.hasProperty('checkstyleVersion') && checkstyleVersion.endsWith("-SNAPSHOT")) {
            mavenLocal {
                content {
                    includeGroup("com.puppycrawl.tools")
                }
            }
        }
    }
}

enableFeaturePreview "TYPESAFE_PROJECT_ACCESSORS"

if (!JavaVersion.current().java11Compatible) {
    throw new GradleException("""\
Configurate requires at least Java 11 to build, but you are running ${JavaVersion.current()}.

Your JAVA_HOME is currently: ${System.getenv("JAVA_HOME")}
Is this correct?
""")
}

def prefix = "configurate"

rootProject.name = "$prefix-parent"

// core
["core", "tool", "bom", "examples"].each {
    include ":$it"
    // findProject(":$it")?.name = "$prefix-$it"
}

// formats
["gson", "hocon", "jackson", "xml", "yaml"].each {
    include ":format:$it"
    // findProject(":format:$it")?.name = "$prefix-$it"
}

// extras
["kotlin", "guice", "dfu2", "dfu3", "dfu4", "interface"].each {
    include ":extra:$it"
    findProject(":extra:$it")?.name = "extra-$it"
}
include ":extra:extra-interface:ap"
findProject(":extra:extra-interface:ap")?.name = "extra-interface-ap"

includeBuild 'vendor', {
  name = "configurate-vendor"
}
includeBuild 'build-logic', {
    name = "configurate-build-logic"
}

